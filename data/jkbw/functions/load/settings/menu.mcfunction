playsound block.note_block.bell player @a[distance=..6]
tag @s add jkbw_admin
function jkbw:load/ticks/load

tellraw @a[tag=jkbw_admin] [{"text":"\n\n\n\n\n\n\n\n\n\n=============","color":"green"},{"text":" 起床战争工具包 ","color":"yellow"},"============\n当前数据包版本：beta1.4.1\n",{"text":"请确认当前服务器的游戏版本是否为1.20.0-1！","color":"yellow"}]
execute if score #test_mode jkbw.mem matches 1 run tellraw @a[tag=jkbw_admin] {"text":"注意：已开启测试模式！","color":"red","bold":true}

execute if score #tutorial jkbw.mem matches ..1 run tellraw @a[tag=jkbw_admin] ["",{"text":"请务必按照教程顺序一步一步操作哦！","color":"aqua"},"\n\n第一步：",{"text":"[使用方法]","color":"gold","clickEvent":{"action":"run_command","value":"/function jkbw:load/settings/tips"},"hoverEvent":{"action":"show_text","value":"※必看※"}}]
execute if score #tutorial jkbw.mem matches 1 run tellraw @a[tag=jkbw_admin] ["\n第二步：",{"text":"[全局传送点]","color":"yellow","clickEvent":{"action":"run_command","value":"/function jkbw:load/settings/menu/worldspawn"},"hoverEvent":{"action":"show_text","value":"建议放在 y > 100 格及其以上（不然没法清理地图）。\n\n准备中的玩家在此点以下会被传送回来，\n游戏开始时会在此点以下平铺一层屏障。\n\n第一次进入游戏时的到达点和死亡传送点，\n又为起床战争地图中心设置边界，半径 100 格。"}}]

execute if score #tutorial jkbw.mem matches 2.. run tellraw @a[tag=jkbw_admin] ["\n第一步：",{"text":"[使用方法]","color":"gold","clickEvent":{"action":"run_command","value":"/function jkbw:load/settings/tips"},"hoverEvent":{"action":"show_text","value":"※必看※"}},"  ",{"text":"[操作台]","color":"dark_red","hoverEvent": {"action":"show_text","value": "一些自定义"},"clickEvent": {"action":"run_command","value": "/function jkbw:load/settings/menu/operation"}},"\n\n第二步：",{"text":"[全局传送点]","color":"yellow","clickEvent":{"action":"run_command","value":"/function jkbw:load/settings/menu/worldspawn"},"hoverEvent":{"action":"show_text","value":"建议放在 y > 100 格及其以上（不然没法清理地图）。\n\n准备中的玩家在此点以下会被传送回来，\n游戏开始时会在此点以下平铺一层屏障。\n\n第一次进入游戏时的到达点和死亡传送点，\n又为起床战争地图中心设置边界，半径 100 格。"}},"  ",{"text":"[清理地图]","color":"blue","clickEvent":{"action":"run_command","value":"/function jkbw:play/ready/map/clear/init"},"hoverEvent":{"action":"show_text","value":"从全局传送点下方开始，\n逐级清理玩家摆放的方块。"}},"  ",{"text":"[转换地图]","color":"white","clickEvent":{"action":"run_command","value":"/function jkbw:play/ready/map/convert/init"},"hoverEvent":{"action":"show_text","value":"从全局传送点下方开始，\n逐级转换会被清除的方块为其他方块。\n开始游戏前有类似方块的需先点击这个！"}},"\n\n第三步：",{"text":"[使用刷怪蛋]","color":"light_purple","clickEvent":{"action":"run_command","value":"/function jkbw:load/settings/menu/eggs"},"hoverEvent":{"action":"show_text","value":"使用时须阅读刷怪蛋和方块上的提示！"}},"  ",{"text":"[出生点检查]","color":"gold","clickEvent":{"action":"run_command","value":"/function jkbw:load/settings/menu/teams/check"},"hoverEvent":{"action":"show_text","value":"内含测试模式"}}]

execute unless score #test_mode jkbw.mem matches 1 run tellraw @a[tag=jkbw_admin] [{"text":"\n==============","color":"green"},{"text":"§m [开始游戏] ","color":"light_purple","hoverEvent":{"action":"show_text","value":"请玩家们使用物品栏最后一格报名！"}},"=============="]
execute if score #test_mode jkbw.mem matches 1 run tellraw @a[tag=jkbw_admin] [{"text":"\n==============","color":"green"},{"text":" [开始游戏] ","color":"light_purple","underlined":true,"clickEvent":{"action":"run_command","value":"/function jkbw:play/start/ready"},"hoverEvent":{"action":"show_text","value":"以测试模式启动"}},"=============="]